#pragma kernel GenerateAllInstances
#pragma kernel CullInvisibleInstances
#pragma kernel resetInstanceCount


#include "noiseSimplex.cginc"

struct FrustumPlane {
    float3 normal;
    float distance;
};


RWStructuredBuffer<float4> _VisibleGrassInstancesBuffer;

RWStructuredBuffer<uint> _ArgsBuffer;

RWStructuredBuffer<FrustumPlane> _FrustumPlanesBuffer;

RWStructuredBuffer<float4> _AllInstancesBuffer;

int _resolution;
int _worldArea;






Texture2D<float4> _heightMapTex;
SamplerState sampler_heightMapTex;
float _displacementStrength;



[numthreads(8,8,1)]
void GenerateAllInstances (uint3 id : SV_DispatchThreadID)
{
    if (id.x <uint(_resolution) && id.y < uint(_resolution))
    {
        float worldScale = 1.0f/float(_resolution);

        float2 uv = float2(id.x, id.y)  * worldScale;

        float4 pos = 0.0f;
        
        pos.y = 1.0f;

        float heightDisplacement = _heightMapTex.SampleLevel(sampler_heightMapTex,uv,0).r;
        pos.y += heightDisplacement * _displacementStrength;
        
        pos.xz = (id.xy - float(_resolution) * 0.5f) * worldScale * _worldArea;

        float noiseFrequency = 0.06f;
        float noiseValue = snoise(pos.xz * noiseFrequency);
        
        pos.w = ((noiseValue + 1.0f) * 0.5f);

        pos.x += noiseValue;
        pos.z += noiseValue;

        _AllInstancesBuffer[id.x + id.y * _resolution] = pos;
        

        
    }
}

[numthreads(8,8,1)]
void CullInvisibleInstances (uint3 id : SV_DispatchThreadID)
{
   
    float4 pos = _AllInstancesBuffer[id.x + id.y * _resolution];
    for (int i = 0; i < 6; i++)
    {
        float ndotplusd = (dot(pos.xyz,_FrustumPlanesBuffer[i].normal ) + _FrustumPlanesBuffer[i].distance);
        if (ndotplusd < 0.0f)
        {
            return;
        }
    }
    

    uint writeIndex;
    InterlockedAdd(_ArgsBuffer[1],uint(3),writeIndex);
    uint instanceIndex = writeIndex/3;
    _VisibleGrassInstancesBuffer[instanceIndex] = pos;
    

    
}

[numthreads(1,1,1)]
void resetInstanceCount (uint3 id : SV_DispatchThreadID)
{
    _ArgsBuffer[1] = 0;
}
